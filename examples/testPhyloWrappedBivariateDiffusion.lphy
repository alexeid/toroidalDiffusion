data {
  l = 100; // sites
  n = 20; // taxa
  taxa = taxa(names=1:n); // taxa names integer must start from 1 for beast xml
  i = n+1;
  j = 2*n-1;
  // tips id starts from 1
  internalNodesID = i:j;
}
model {
  λ ~ LogNormal(meanlog=3.0, sdlog=0.1); //sd = 0.1
  ψ ~ Yule(lambda=λ, taxa=taxa);
  ψ2 = setInternalNodesID(tree = ψ, internalNodesID = internalNodesID);

  // angle is [0, 2pi)
  // 0.7557974 2.0137527 5.3654594
  mu ~ LogNormal(meanlog=0.7, sdlog=0.5, replicates = 2);
  // 0.1408635 1.0000000 7.0990714
  σ ~ LogNormal(meanlog=0, sdlog=1, replicates = 2);
  // 0.3396016 0.9048374 2.4108563
  drift ~ LogNormal(meanlog=-0.1, sdlog=0.5, replicates = 2);
  // correlation
  driftCorr ~ Uniform(lower=-1.0, upper=1.0);

  // the root sequence is sampled from equilibrium distribution
  X ~ PhyloWrappedBivariateDiffusion(mu=mu, sigma=σ, drift=drift, driftCorr=driftCorr, L=l, tree=ψ2);
}


